#!/usr/bin/perl -w
#
# Daybo Logic Podcast downloader
# Copyright (c) 2012-2013, David Duncan Ross Palmer, Daybo Logic
# All rights reserved.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are met:
#
#     * Redistributions of source code must retain the above copyright notice,
#       this list of conditions and the following disclaimer.
#
#     * Redistributions in binary form must reproduce the above copyright
#       notice, this list of conditions and the following disclaimer in the
#       documentation and/or other materials provided with the distribution.
#
#     * Neither the name of the Daybo Logic nor the names of its contributors
#       may be used to endorse or promote products derived from this software
#       without specific prior written permission.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
# AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
# IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
# ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
# LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
# CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
# SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
# INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
# CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
# ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
# POSSIBILITY OF SUCH DAMAGE.

package main;
use LWP::UserAgent;
use XML::Feed;
use Data::Dumper;
use Daybo::ConfReader;
use Getopt::Std;
use URI::Escape;
use IO::Interactive qw(is_interactive);
use strict;
use warnings;
use diagnostics;

my $Debug = 0;

sub FileFromURI($)
{
	my $Url = shift;
	return undef if ( !$Url );
	my @uri_parts = split('/', $Url);
	return $uri_parts[-1];
}

sub ReadFeed($$$)
{
	my @entries = ( );
	my ( $Feeds, $Url, $Name ) = @_;
	my $feed = XML::Feed->parse(URI->new($Url));
	if ( !$feed ) {
		printf(STDERR "Stream %s error: %s\n", $Name, $@);
		return @entries;
	}

	foreach my $entry ($feed->entries) {
		my $enclosure = $entry->{'entry'}->{'enclosure'};
		my $uriDecoded = uri_unescape($enclosure->{'url'});
		my $filename = FileFromURI($uriDecoded);
		push(@entries, {
			filename => $filename,
			uri      => $uriDecoded,
			title    => $entry->title,
			type     => $enclosure->{'type'},
			length   => $enclosure->{'length'}
		}) if ( $filename );
	}
	return @entries;
}

sub DownloadStream($$$)
{
	my ( $ua, $f, %get_params, $is_interactive );
	my $local_length = 0;
	my ( $Feeds, $entry, $Name ) = @_;
	my $local_podcast = $Feeds->{$Name}->{localpath} . '/' . $entry->{filename};
	if ( -f $local_podcast ) {
		$local_length = (stat($local_podcast))[7];
		if ( !$entry->{length} || $local_length == $entry->{length} ) {
			printf(STDERR "%s already exists\n", $local_podcast);
			return;
		}
	}
	$is_interactive = is_interactive();
	$ua = LWP::UserAgent->new( show_progress => $is_interactive );
	printf(STDERR "Downloading %s ... ", $entry->{uri}) if ( !$is_interactive );

	unless ( $Feeds->{_main}->{noop} ) {
		if ( open($f, '>>:raw', $local_podcast) ) {
			binmode($f);
		} else {
			printf(STDERR "Local failure on stream %s -- %s: %s\n", $Name, $local_podcast, $!);
			return;
		}
	}
	%get_params = (
		'Range' => "bytes=$local_length-"
	) if ( $local_length );
	$ua->get(
		$entry->{uri},
		%get_params,
		':content_cb' => sub {
			my ( $chunk ) = @_;
			print $f $chunk unless ( $Feeds->{_main}->{noop} );
		}
	);
	close($f) if ( !$Feeds->{_main}->{noop} );
	printf(STDERR "done.\n") if ( !$is_interactive );
}

sub ProcessTags($$)
{
	my ( $Feeds, $V ) = @_;
	my $tagRx = qr/^\$([A-Z0-9]+)/o;
	my $avoid = 0;
	while ( (my $idx = index($V, '$', $avoid)) > -1 ) { # Find remaining user-variable references
		$avoid = 0;
		my $var = substr($V, $idx);
		if ( $var =~ $tagRx ) {
			my $v = $Feeds->{_main}->{ uc($1) };
			warn(sprintf('%s -> %s', $1, $v || '(undef)')) if ( $Debug );
			if ( !defined($v) ) {
				$avoid = $idx+1;
				next;
			}
			substr($V, $idx, length($1)+1, $v);
		} else {
			$avoid++;
		}
	}
	return $V;
}

sub main()
{
	my $conf;
	my @entries = ( );
	my @defKeys;
	my %feeds = ( );
	my %opts = ( );
	my $confSeen = 0;
	my @confFiles = (
		'dlpodget.rc',
		"$ENV{HOME}/.dlpodget.rc"
	);

	return 1 if ( !getopts('dn:', \%opts) );
	$Debug = 1 if ( $opts{'d'} );
	print(STDERR "Explicit debug mode enabled by -d\n") if ( $Debug );

	$conf = Daybo::ConfReader->new();
	foreach my $confFile ( @confFiles ) {
		next unless ( -f $confFile );
		if ( $conf->SetFn($confFile) ) {
			$conf->Reload();
			$confSeen++;
			last;
		}
	}
	$conf = undef if ( !$confSeen );

	if ( $conf && $conf->GetKeys(undef, \@defKeys) ) {
		if ( 'enable' ~~ @defKeys ) {
			if ( !$conf->GetDatum(undef, 'enable') ) {
				print(STDERR "Configuration disabled.\n");
				$conf = undef;
			}
		}
	}
	if ( $conf ) {
		my %reserveSec = map {  $_ => 1 } ( '_main', 'paths' ); # Reserved section names
		my ( @sections, $secC );
		$secC = $conf->GetSections(\@sections);

		# First load the generation configuration information
		# Include environment variables.
		foreach my $ek ( keys(%ENV) ) {
			$feeds{_main}->{ uc($ek) } = $ENV{$ek};
		}
		if ( scalar(@defKeys) ) {
			foreach my $mk ( @defKeys ) {
				$feeds{_main}->{ uc($mk) } = $conf->GetDatum(undef, $mk);
			}
			# Set debug flag via config if it existed.
			if ( $feeds{_main}->{debug} ) {
				$Debug = $feeds{_main}->{debug};
				printf(STDERR "Set Debug via config: %s\n", $Debug);
			}
		}

		if ( 'paths' ~~ @sections ) { # Has a paths section?
			my @secKeys;
			if ( $conf->GetKeys('paths', \@secKeys) ) {
				foreach my $pk ( @secKeys ) {
					$feeds{_main}->{ uc($pk) } = $conf->GetDatum('paths', $pk);
				}
				# For backward compatibility, set localpfx with [paths] root
				$feeds{_main}->{'LOCALPFX'} = $feeds{_main}->{'ROOT'}
					if ( $feeds{_main}->{'ROOT'} );
			}
		}

		for ( my $secI = 0; $secI < $secC; $secI++ ) {
			my @secKeys;
			next if ( $opts{'n'} && $sections[$secI] ne $opts{'n'} );
			my $keyC = $conf->GetKeys($sections[$secI], \@secKeys);
			foreach ( my $keyI = 0; $keyI < $keyC; $keyI++ ) {
				next if ( $reserveSec{$sections[$secI]} ); # Skip reserved sections
				my $v = $conf->GetDatum($sections[$secI], $secKeys[$keyI]);
				$v = ProcessTags(\%feeds, $v);
				printf(
					STDERR
					'[%s] %s -> %s'."\n",
					$sections[$secI],
					$secKeys[$keyI],
					$v
				) if ( $Debug );
				$feeds{ $sections[$secI] }->{ $secKeys[$keyI] } = $v;
			}
		}
	}

	foreach my $feedName ( keys(%feeds) ) {
		my $feed = $feeds{$feedName};
		next if ( !$feed->{enable} );
		next if ( !$feed->{download} );
		@entries = ReadFeed(\%feeds, $feed->{rss}, $feedName);
		foreach my $entry ( @entries ) {
			print STDERR 'Processing entry: ' . Dumper $entry if ( $Debug );
			DownloadStream(\%feeds, $entry, $feedName);
		}
	}

	return 0;
}

exit(main());
