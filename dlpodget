#!/usr/bin/perl -w

package main;
use LWP::Simple qw(get);
use XML::Feed;
use Data::Dumper;
use Getopt::Std;
use strict;
use warnings;
use diagnostics;

use constant URL              => 'http://xml.nfowars.net/Alex.rss';
use constant LOCAL_STORE_PATH => '/home/overlord/infowars';

my $Debug = 0;

sub FileFromURI($)
{
	my $Url = shift;
	my @uri_parts = split('/', $Url);
	return $uri_parts[-1];
}

sub ReadStream($)
{
	my @entries = ( );
	my $Url = shift;
	my $feed = XML::Feed->parse(URI->new($Url));
	foreach my $entry ($feed->entries) {
		my $filename = FileFromURI($entry->link);
		push(@entries, {
			filename => $filename,
			uri      => $entry->link
		});
	}
	return @entries;
}

sub DownloadStream($)
{
	my $data;
	my $entry = shift;
	my $local_podcast = LOCAL_STORE_PATH() . '/' . $entry->{filename};
	if ( -f $local_podcast ) {
		printf(STDERR "%s already exists\n", $local_podcast);
		return;
	}
	printf(STDERR "Downloading %s ... ", $entry->{uri});
	$data = get($entry->{uri});
	print(STDERR "done.\n");

	printf(STDERR "\nWriting %s ... ", $local_podcast);
	if ( open(my $f, '>', $local_podcast) ) {
		binmode($f);
		print($f $data);
		close($f);
		print(STDERR "done.\n");
	} else {
		printf(STDERR "Failed -- %s\n", $!);
	}
}

sub main()
{
	my @entries = ( );
	my %opts = ( );
	return 1 if ( !getopts('d', \%opts) );
	$Debug = 1 if ( $opts{'d'} );
	print(STDERR "Explicit debug mode enabled by -d\n") if ( $Debug );
	@entries = ReadStream(URL());
	foreach my $entry ( @entries ) {
		DownloadStream($entry);
	}
	return 0;
}

exit(main());
